model{
for (i in 1:I){	#individuals
	temp[i]<-(log(timeF[i]+0.01)-log(lam[i]))/sigma
	log(lam[i])<-beta[1]+beta[2]*x1[i]+beta[3]*x2[i]+beta[4]*x3[i]+beta[5]*x4[i]+
		u[spt[i]]	#spt is a variable indicating which spatial region individual i is in
#normal distn surv and density
	s[i]<-1-phi(temp[i])
	f[i]<-pow(2*3.14,-0.5)*exp(-0.5*pow(temp[i],2))
#log like
	L[i]<-indic[i]*log(f[i]/(sigma*(timeF[i]+0.01)))+(1-indic[i])*log(s[i])
#Poisson zeros trick
	zeros[i]<-0
	new[i]<- -L[i]
	zeros[i]~dpois(new[i])
}#close i loop
for (j in 1:5){
	beta[j]~dnorm(0,0.001)
}
tauu<-pow(sdu,-2)
sdu~dunif(0,10)
sigma~dunif(0.01,10)
#spatial random effects
for (j in 1:25){	#spatial regions
	u[j]~dnorm(0,tauu)
}#close j loop
}#close model loop