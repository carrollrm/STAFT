model{
for (i in 1:I){	#individuals
	temp[i]<-(log(timeF[i]+0.01)-log(lam[i]))/sigma
	log(lam[i])<-beta[1]+beta[2]*x1[i]+beta[3]*x2[i]+beta[4]*x3[i]+beta[5]*x4[i]+
		u[spt[i]]+gam[yr[i]]
#extreme value distn surv and density
	s[i]<-exp(-exp(temp[i]))
	f[i]<-s[i]*exp(temp[i])
#log like
	L[i]<-indic[i]*log(f[i]/(sigma*(timeF[i]+0.01)))+(1-indic[i])*log(s[i])
#WAIC calcs
	ppoc[i]<-exp(indic[i]*log(f[i]/(sigma*(timeF[i]+0.01)))+(1-indic[i])*log(s[i]))
	icpoc[i]<-1/ppoc[i]
	logppoc[i]<-log(ppoc[i])
#Poisson zeros trick
	zeros[i]<-0
	new[i]<- -L[i]
	zeros[i]~dpois(new[i])
}#close i loop
for (j in 1:5){
	beta[j]~dnorm(0,0.001)
}
tauu<-pow(sdu,-2)
sdu~dunif(0,10)
sigma~dunif(0.01,10)
#spatial random effects
for (j in 1:25){	#spatial regions
	u[j]~dnorm(0,tauu)
}#close j loop
gam[1]<-0
taugam<-pow(sdgam,-2)
sdgam~dunif(0,10)
for (j in 2:6){
	gam[j]~dnorm(gam[j-1],taugam)
}#close j loop
}#close model loop